module string

use core.array.{Array, ArrayIter}
use core.boolean.boolean
use core.collections.Hash
use core.interfaces.Default
use core.iterate.{AsIterator, Iterable}
use core.maybe.some
use core.numeric.i32
use core.ops.{Addition}
use core.sentinels.{nonfatal, todo}

public use display.Display
public use coloredstring.ColoredString
public use template.Template


var CONT_MASK = 0b00111111
var PADDING_CHARACTER = 0x20

/// Model for creating, manipulating and formatting text sequences.
public model String 
    implements Addition 
    + AsIterator<String, Chars> 
    + Default
    + Display
    + Hash
{
    var vec: []i32

    new(vec: []i32) {
        this.vec = vec
    }

    /// Implementation of the Display trait.
    public function [Display.toString] -> String {
       return '"' + this.replace('"', '\\"') + '"'
    }
    /// Implementation of the Default trait.
    static function [Default.init] -> This {
        return String([])
    }
    /// Adds two strings together.
    /// ## Usage
    /// ```
    /// var str = "Hello" + ", world!"
    /// assert(str).equals("Hello, world!")
    /// ```
    public function [Addition.add](other: This) -> This {
        var newString = String.fromUtf8(this.vec + [])
        newString.vec.append(this.vec)
        return newString
    }
    /// Creates an iterator from the contents of a String.
    /// ## Usage
    /// ```
    /// var str = "This is a String"
    /// for char in str {
    ///     print(char)
    /// }
    /// ```
    public function [AsIterator.iter] -> Chars {
        return Chars(this.vec.iter())
    }
    /// Adds text to the end of the String.
    /// ## Usage
    /// ```
    /// var str = "Hello"
    /// str.append(", world!")
    /// 
    /// assert(str).equals("Hello, world!")
    /// ```
    public function append(suffix: String) {
        this.vec.append(suffix.vec)
    }
    /// Converts the String to an array of bytes.
    /// ### Usage
    /// ```
    /// ```
    public function bytes -> []i32 {
        return this.vec + []
    }
    /// Changes the first character of every word in the String to upper case.
    /// ## Usage
    /// ```
    /// var str = "jonathan is a boy"
    /// var str2 = str.capitalCase()
    /// 
    /// assert(str2).equals("Jonathan Is A Boy")
    /// ```
    public function capitalCase -> String {
        var result = String.withCapacity((this.vec.length())) 
        var isNewWord = true // Flag to track the start of a new word 

        for char in this.characters() {
            if char.isWhitespace() {  
                isNewWord = true
                result += char 
            } else {
                if isNewWord {
                    result += char.upperCase()
                    isNewWord = false
                } else {
                    result += char
                }
            }
        }
        return result 
    }
    /// Returns an iterator over the valid characters in the String.
    /// ## Usage
    /// ```
    /// var str = "adebola.io"
    /// 
    /// for char in str.characters() {
    ///     print(char)
    /// }
    /// ```
    public function characters -> Chars {
        return Chars(this.vec.iter())
    }
    /// Empties the String of its characters.
    /// ## Usage
    /// ```
    /// var str = "tralalalalaleeday"
    /// str.clear()
    /// 
    /// assert(str).equals("")
    /// assert(str.length()).equals(0)
    /// ```
    public function clear {
        this.vec = Array(this.vec.capacity())
    }
    /// Returns true if a substring is contained within the String.
    /// ## Usage
    /// ```
    /// var str = "This is a world premiere."
    /// 
    /// assertThat(str.contains("world"))
    /// assertThat(str.contains("premiere"))
    /// ```
    public function contains(substr: String) -> boolean {
       return this.vec.includes(substr.vec)
    }
    /// Returns true if the String ends with a given pattern.
    /// ### Usage
    /// ```
    /// var name = "Michael"
    /// assertThat(name.endsWith("l"))
    /// assertThat(name.endsWith("ael"))
    /// ```
    public function endsWith(pat: String) -> boolean {
        var patternLength = pat.vec.length()
        var stringLength = this.vec.length() 
        if patternLength > stringLength {
            return false
        }
        var i = stringLength - 1
        while i > patternLength {
            if this.vec[i] != pat.vec[i] {
                return false
            }
            i -= 1
        }
        return true
    }
    /// Returns true if the String is made up entirely of (ASCII) alphabetic characters.
    /// ## Usage
    /// ```
    /// var str = "abcdefghijklmnopqrstuvwxyz"
    /// assertThat(str.isAsciiAlphabetic())
    /// 
    /// var str2 = "123"
    /// assertThat(!str2.isAsciiAlphabetic())
    /// ```
    public function isAsciiAlphabetic -> boolean {
        for character in this.characters() {
            if character.vec.length() > 1 {
                return false
            }
            var codePoint = character.vec[0]
            if (codePoint < 65 or (codePoint > 90 and codePoint < 97) or codePoint > 122) {
                return false
            }
        }
        return false
    }
    /// Returns true if the String is made up entirely of (ASCII) numeric characters.
    /// ## Usage
    /// ```
    /// var str2 = "123"
    /// assertThat(str2.isAsciiNumeric())
    /// 
    /// var str = "abcdefghijklmnopqrstuvwxyz"
    /// assertThat(!str.isAsciiNumeric())
    /// ```
    public function isAsciiNumeric -> boolean {
        for character in this.characters() {
            if character.vec.length() > 1 {
                return false
            }
            var codePoint = character.vec[0]
            if (codePoint < 48 or codePoint > 57) {
                return false
            }
        }
        return false
    }
    /// Returns true if the String is made up entirely of (ASCII) alphabetic and numeric characters.
    /// ## Usage
    /// ```
    /// var str = "helloworld1234"
    /// assertThat(str.isAsciiAlphanumeric())
    /// 
    /// var str2 = "%hello_world$"
    /// assertThat(!str2.isAsciiAlphanumeric())
    /// ```
    public function isAsciiAlphanumeric -> boolean {
        for character in this.characters() {
            if character.vec.length() > 1 {
                return false
            }
            var codePoint = character.vec[0]
            if (codePoint < 48) 
                or (codePoint > 57 and codePoint < 65)
                or (codePoint > 90 and codePoint < 97)
                or codePoint > 122
            {
                return false
            }
        }
        return false
    }
    /// Returns true if the String is made up entirely of whitespace characters.
    /// ### Usage
    /// ```
    /// var str = "     "
    /// assertThat(str.isWhitespace())
    /// ```
    public function isWhitespace -> boolean {
        todo()
    }
    /// Returns true if the String has no characters.
    /// ## Usage
    /// ```
    /// var str = ""
    /// assertThat(str.isEmpty())
    /// 
    /// str.append("Hello")
    /// assertThat(!str.isEmpty())
    /// ```
    public function isEmpty -> boolean {
        return this.vec.length() == 0
    }
    /// Returns the number of bytes in the UTF-8 encoded String.
    /// 
    /// **Important:** For non-ASCII strings, the number of bytes will not 
    /// directly correspond to the number of characters due to multibyte characters. 
    /// 
    /// To count the number of characters in a String, use the `str.characters().count()` function. 
    /// ### Usage
    /// ```
    /// var str = "Hello" 
    /// assert(str.length()).equals(5) // 5 bytes
    /// 
    /// var str2 = "こんにちは"  
    /// assert(str2.length()).equals(9) // 9 bytes, but 3 characters
    /// 
    /// assert(str.characters().count()).equals(5)
    /// assert(str2.characters().count()).equals(3)
    /// ```
    public function length -> i32 {
        return this.vec.length()
    }
    /// Changes the case of all ASCII letters in the String to lower case.
    /// ## Usage
    /// ```
    /// var str = "HELLO, WORLD!"
    /// var str2 = str.lowerCase()
    /// 
    /// assert(str2).equals("hello, world!")
    /// ```
    public function lowerCase -> String {
        var result = String.withCapacity(this.vec.length()) 
        for char in this.characters() {
            if char.isAsciiAlphabetic() and char.vec[0] >= 65 and char.vec[0] <= 90 { 
                result.vec.push(char.vec[0] + 32)
            } else {
                result.vec.append(char.vec)
            }
        }
        return result 
    }
    /// Returns a version of the String with padding on both sides.
    /// ## Usage
    /// ```
    /// var str = "Alright"
    /// var str2 = str.pad(3)
    /// 
    /// assert(str2).equals("   Alright   ") // 3 spaces before and after.
    /// ```
    public function pad(count: i32) -> String {
        var paddingCount = if count < 0 {
            nonfatal("InvalidArgument: Padding count cannot be negative. Defaulting to zero.")
            0
        } else {
            count
        }

        var currentLength = this.vec.length()
        var totalLength = currentLength + paddingCount * 2
        var paddedString = String.withCapacity(totalLength)

        var i = 0 
        while i < totalLength {
            if i < paddingCount || i >= currentLength + paddingCount {
                // We're in the padding section
                paddedString.vec[i] = PADDING_CHARACTER
            } else {
                // We're in the original content section
                paddedString.vec[i] = this.vec[i - paddingCount]
            }
            i += 1
        }

        return paddedString
    }
    /// Returns a version of the String with left padding.
    /// ## Usage
    /// ```
    /// var str = "Alright"
    /// var str2 = str.padLeft(3)
    /// 
    /// assert(str2).equals("   Alright") // 3 spaces before.
    /// ```
    public function padLeft(count: i32) -> String {
        var paddingCount = if count < 0 {
            nonfatal("InvalidArgument: Padding count cannot be negative. Defaulting to zero.")
            0
        } else {
            count
        }

        var currentLength = this.vec.length()
        var totalLength = currentLength + paddingCount
        var paddedString = String.withCapacity(totalLength)

        var i = 0 
        while i < totalLength {
            if i < paddingCount {
                paddedString.vec[i] = PADDING_CHARACTER
            } else {
                paddedString.vec[i] = this.vec[i - paddingCount]
            }
            i += 1
        }

        return paddedString
    }
    /// Returns a version of the String with left padding.
    /// ## Usage
    /// ``` 
    /// var str = "Alright"
    /// var str2 = str.padRight(3)
    /// 
    /// assert(str2).equals("Alright   ") // 3 spaces before.
    /// ```
    public function padRight(count: i32) -> String {
        var paddingCount = if count < 0 {
            nonfatal("InvalidArgument: Padding count cannot be negative. Defaulting to zero.")
            0
        } else {
            count
        }

        var currentLength = this.vec.length()
        var totalLength = currentLength + paddingCount * 2
        var paddedString = String.withCapacity(totalLength)

        var i = 0 
        while i < totalLength {
            if i >= currentLength + paddingCount {
                paddedString.vec[i] = PADDING_CHARACTER
            } else {
                paddedString.vec[i] = this.vec[i - paddingCount]
            }
            i += 1
        }

        return paddedString
    }
    /// Pads the String in place with whitespace on the left and right sides.
    /// ## Usage
    /// ```
    /// var str = "Hello, world!"
    /// str.padInPlace(3)
    /// 
    /// assert(str).equals("   Hello, world!   ")
    /// ```
    public function padInPlace(count: i32) {
        this = this.pad(count)
    }
    /// Adds text to the start of the String.
    /// ## Usage
    /// ```
    /// var str = "world!"
    /// str.prepend("Hello, ")
    /// 
    /// assert(str).equals("Hello, world!")
    /// ```
    public function prepend(prefix: String) {
        var newString = prefix
        newString += this
        this = newString
    }
    /// Returns a String value that is made from a number of copies of the original String.
    /// If count is 0, the empty String is returned.
    /// ## Usage
    /// ```
    /// var str = "Whirlwind"
    /// 
    /// var str2 = str.repeat(3)
    /// assert(str2).equals("WhirlwindWhirlwindWhirlwind") // 3 copies.
    /// 
    /// var str3 = str.repeat(1)
    /// assert(str3).equals("Whirlwind") // One copy.
    /// 
    /// var str3 = str.repeat(0)
    /// assertThat(str2.IsEmpty()) // No copies.
    /// ```
    public function repeat(count: i32) -> String {
        var str: String
        for {} in 0..count {
           str.append(this)
        }
        return str
    }
    /// Replaces all instances of a substring and returns the new String.
    /// ## Usage
    /// ```
    /// var str = "John goes to school. John buys a book."
    /// 
    /// var str2 = str.replace("John", "Mike")
    /// 
    /// assert(str2).equals("Mike goes to school. Mike buys a book.")
    /// ```
    public function replace(subStr: String, newStr: String) -> String {
        todo()
    }
    /// Returns a reversed version of the String.
    /// ## Usage
    /// ```
    /// var str = "pandemonium"
    /// 
    /// var str2 = str.reverse()
    /// 
    /// assert(str2).equals("muinomednap")
    /// ```
    public function reverse -> String {
        var newString = String.withCapacity(this.vec.length())
        for char in this.characters() {
            newString = char + newString
        }
        return newString
    }
    /// Returns a section of the String.
    /// The range arguments are exclusive, meaning it goes from the left index all the way to one before the right.
    /// ## Usage
    /// ```
    /// var str = "Hello, world!"
    /// 
    /// assert(str.slice(0, some(5))).equals(some("Hello")) // index 0 to 4.
    /// ```
    public function slice(start: i32, end?: i32) -> ?String {
        todo()
    }
    /// Divides the String into a list of substrings based on a delimeter.
    /// ## Usage
    /// ```
    /// var str = "Ring around the rosy"
    /// var words = str.split(" ")
    /// 
    /// assert(words == ["Ring", "Around", "The", "Rosy"])
    /// ```
    public function split(delim: String) -> []String {
        todo()
    }
    /// Divides the String into a list of substrings based on a predicate.
    /// ## Usage
    /// ```
    /// var str = "Hello world. My name is James."
    /// var sentences = str.splitAt(fn(pat) pat == ". ")
    /// 
    /// assert(sentences).equals(["Hello world", "My name is James."])
    /// ```
    public function splitAt(predicate: fn(pattern: String) -> boolean) -> []String {
        todo()
    }
    /// Changes only the first character in the String to upper case.
    /// ## Usage
    /// ```
    /// var str = "nice to meet you, where you been?"
    /// var str2 = str.sentenceCase()
    /// 
    /// assert(str2).equals("Nice to meet you, where you been?")
    /// ```
    public function sentenceCase -> String {
        var result = String.withCapacity(this.vec.length())
        var stringLength = this.vec.length()
        var maybeFirstCharacter = this.characters().next()
        if maybeFirstCharacter.isNone() {
            return ""
        }
        result += maybeFirstCharacter.unwrap().upperCase()
        var i = result.vec.length() // length of first character.
        while i < stringLength {
            result.vec.push(this.vec[i])
            i += 1
        }
        return result
    }
    /// Returns a version of the String without left or right whitespace padding.
    /// ## Usage
    /// ```
    /// var str = "\n\n\nWhat then shall we say?  \n\n\n\r\t"
    /// 
    /// assert(str.trim()).equals("What then shall we say?")
    /// ```
    public function trim -> String {
        todo()
    }  
    /// Mutates the String in place and removes left or right whitespace padding.
    /// ## Usage
    /// ```
    /// var str = "\n\n\nWhat then shall we say?  \n\n\n\r\t"
    /// 
    /// str.trimInPlace()
    /// 
    /// assert(str).equals("What then shall we say?")
    /// ```
    public function trimInPlace {
        this = this.trim()
    }   
    /// Returns a version of the String without left whitespace padding.
    /// ## Usage
    /// ```
    /// var str = "\n\n\nWhat then shall we say?\n"
    /// 
    /// assert(str.trimStart()).equals("What then shall we say?\n")
    /// ```
    public function trimStart -> String {
        todo()
    } 
    /// Returns a version of the String without right whitespace padding.
    /// ## Usage
    /// ```
    /// var str = "\nWhat then shall we say?\n\n\n"
    /// 
    /// assert(str.trimEnd()).equals("\nWhat then shall we say?")
    /// ```
    public function trimEnd -> String { 
       todo()
    }
    /// Changes the case of all letters in the String to upper case.
    /// ## Usage
    /// ```
    /// var str = "hello, world!"
    /// assert(str.upperCase()).equals("HELLO, WORLD!")
    /// ```
    public function upperCase -> String {
        var result = String.withCapacity(this.vec.length())
        for char in this.characters() {
            if char.isAsciiAlphabetic() and char.vec[0] >= 97 and char.vec[0] <= 122 {
                result.vec.push(char.vec[0] - 32)
            } else {
                result.vec.append(char.vec)
            }
        }
        return result 
    }
    /// Returns true if the String starts with a given pattern.
    /// 
    /// ### Usage
    /// ```
    /// var name = "Joshua"
    /// assertThat(name.startsWith("J"))
    /// assertThat(name.startsWith("Jo"))
    /// ```
    public function startsWith(pat: String) -> boolean {
        var patternLength = pat.vec.length()
        var stringLength = this.vec.length() 
        if patternLength > stringLength {
            return false
        }
        var i = 0
        while i < patternLength {
            if this.vec[i] != pat.vec[i] {
                return false
            }
            i += 1
        }
        return true
    }
    /// Creates a String with at least the specified capacity.
    /// ### Usage
    /// ```
    /// ```
    public static function withCapacity(capacity: i32) -> This {
        return String(Array(capacity))
    }
    /// Creates a String from an array of bytes.
    /// ### Usage
    /// ```
    /// ```
    public static function fromUtf8(vec: []i32) -> This {
        return String(vec)
    }
    public static function fromCodePoint(point: i32) -> This {
        return String([point])
    }
    /// Creates a String from a value that implements Display.
    /// ### Usage
    /// ```
    /// var value = 1234
    /// var valueStr = String.from(value)
    /// 
    /// assert(valueStr).equals("1234")
    /// ```
    public static function from<T implements Display>(value: T) -> This {
       return value.toString()
    }
    /// Creates a new formattable String template.
    /// ### Usage
    /// ```
    /// var greeting = String.fmt("Hello, %!")
    /// var greetingStr = greeting.add("John").finish()
    /// 
    /// assert(greetingStr).equals("Hello, John!")
    /// ```
    public static function fmt(template: String) -> Template {
        return Template(template)
    }
}


/// A simple iterator over the characters in a String.
public model Chars implements Iterable<String> {
    var idx: i32
    var bytes: ArrayIter<i32>
    
    new(bytes: ArrayIter<i32>) {
        this.idx = 0
        this.bytes = bytes
    }
    /// Returns the next character in the String.
    public function [Iterable.next] -> ?String {
        return nextCodePoint(this.bytes).map(fn(ch) String.fromUtf8([ch]))
    }
}

// The algorithm is from the Rust standard library.
public function nextCodePoint(bytes: ArrayIter<i32>) -> ?i32 {
    // Decode UTF-8
    var x = bytes.next()?
    if x < 128 {
        return some(x)
    }

    // Multibyte case follows
    // Decode from a byte combination out of: [[[x y] z] w]
    // NOTE: Performance is sensitive to the exact formulation here
    var init = utf8FirstByte(x, 2)
    // SAFETY: `bytes` produces an UTF-8-like String,
    // so the iterator must produce a value here.
    var y = bytes.next().unwrap()
    var ch = utf8AccContByte(init, y)
    if x > 0xE0 {
        // [[x y z] w] case
        // 5th bit in 0xE0 .. 0xEF is always clear, so `init` is still valid
        // SAFETY: `bytes` produces an UTF-8-like String,
        // so the iterator must produce a value here.
        var z = bytes.next().unwrap()
        var y_z = utf8AccContByte((y & CONT_MASK), z)
        ch = init << 12 | y_z
        if x >= 0xF0 {
            // [x y z w] case
            // use only the lower 3 bits of `init`
            // SAFETY: `bytes` produces an UTF-8-like String,
            // so the iterator must produce a value here.
            var w = bytes.next().unwrap() 
            ch = (init & 7) << 18 | utf8AccContByte(y_z, w)
        }

    }

    some(ch)
}

function utf8FirstByte(byte: i32, width: i32) -> i32 {
    return (byte & (0x7F >> width))
}

/// Returns the value of `ch` updated with continuation byte `byte`.
function utf8AccContByte(ch: i32, byte: i32) -> i32 {
    ((ch << 6) | (byte & CONT_MASK))
}

/// Represents an interface for models that can be created from a String representation.
public interface FromStr {
    /// Parses a provided String and returns an instance of the implementing type.
    public static function fromString(str: String) -> ?This
}