/// Contains the primitive `Bool` model for representing
/// boolean values.
module bool

use core.maybe.{none, some}
use core.interfaces.Default
use core.string.{Display, FromStr, String}

/// A representation of boolean values, `true` or `false`.
public model Bool 
    implements Display 
    + Default 
    + FromStr 
{
    static function [Default.init] -> This {
        return false
    }

    public function [Display.toStr] -> String {
        if this { "true" } else "false"
    }

    public static function [FromStr.fromStr](string: String) -> ?This {
        if string == "true" { some(true) } 
        else if string == "false" { some(false) }
        else none()
    }
    /// Creates a `Maybe<T>` based on the value.
    /// ### Usage
    /// ```
    /// var operationSuccessful = true
    /// var message = fn "Operation successful!"
    /// 
    /// var optionalMessage = operationSuccessful.ifTrue(message)
    /// 
    /// assertThat(optionalMessage.isSome())
    /// ```
    public function ifTrue<T>(consequent: fn -> T) -> ?T {
        if this { some(consequent()) } else { none() }
    }
}

