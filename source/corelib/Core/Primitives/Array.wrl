module Array;

use Bool.Bool;
use Numeric.{ Int, UnsignedInt };
use Ops.{ Orderable, Addition };
use Super.Prelude.{ AsIterator, Maybe, None, Some, Todo, String, Default };
use Super.Prelude.Iteratable;
use Super.Prelude.Display;
use Super.Math.{ Max, Min };
use Super.Internals.{ Injunction, Invoke };

/// A contiguous, growable list of items.
public model Array<T> implements AsIterator<T, ArrayIter<T>> {
    public function [AsIterator.IntoIter](): ArrayIter<T> {
        new ArrayIter(this)
    }
    /// Returns an iterator over references to the elements in this array.
    /// ### Usage
    /// ```
    /// ```
    public function Iter(): ArrayRefIter<T> {
        new ArrayRefIter(this)
    }
    /// Returns the item at a particular index in the array, if it exists.
    /// Indexes are zero-based, meaning that the first item is at index 0.
    /// ### Usage
    /// ```
    /// arr := [1, 2, 3, 4];
    /// Assert(arr.At(0)).Equals(Some(1));
    /// Assert(arr.Ar(1)).Equals(Some(2));
    /// ```
    /// Elements can also be indexed from behind the array.
    /// Backward indexes are one-based, meaning the index -1 is the
    /// last item in the array.
    /// ```
    /// arr := [15, 25, 35, 45, 55];
    /// Assert(arr.At(-1)).Equals(Some(55))
    /// ```
    public function At(index: Int): Maybe<&T> {
        length := this.Length();
        i := if index < 0 {
            length + index
        } else {
            index
        };
        if i > length or i < 0 {
            return None();
        };
        a := this[i.Unsigned()];
        return Some(&a);
    }
    /// Checks if an item is contained in the array.
    /// ### Usage
    /// ```
    /// str := "Hello, world";
    /// array := [];
    /// array.Push(str);
    /// 
    /// AssertThat(array.Contains(&str));
    /// ```
    /// The method checks referential equality, meaning it will only return true
    /// if the checked item is in the array,
    /// and **not** if it is equivalent to an item in the array.
    /// 
    /// ```
    /// num := 24;
    /// array := [24];
    /// 
    /// AssertThat(array.Contains(&num)) // This will fail, because the 24 in the array is different from the num 24.
    /// ```
    /// To check for comparative equality, consider using `.Iter().Any(...)`.
    public function Contains(value: &T): Bool {
        this.IntoIter().Any(fn(element) element is value)
    }
    /// Removes all elements in the array.
    /// ### Usage
    /// ```
    /// arr := [1, 2, 3, 4];
    /// 
    /// arr.Clear();
    /// AssertThat(arr.Length() == 0);
    /// AssertThat(arr == []);
    /// ```
    public function Clear() {
        *this = [];
    }
    /// Returns a reference to the first item in the array, if it exists.
    public function First(): ?&T {
        if this.Length() == 0 { None() } 
        else Some(&this[0])
    }
    /// Returns the zero-based index of a value in the array.
    /// 
    /// Like the `.Contains` method, this method checks 
    /// for referential equality.
    /// ### Usage
    /// ```
    /// str1 := "Hello, world!";
    /// str2 := "Welcome to a new era!";
    /// str3 := "This is the future!";
    /// 
    /// arr := [str1, str2, str3];
    /// Assert(arr.IndexOf(&str2)).Equals(Some(1)).
    /// ```
    public function IndexOf(value: &T): ?UnsignedInt {
        this.IntoIter()
            .Enumerate()
            .Find(fn(tuple) tuple.two is value)
            .Map(fn(tuple) tuple.one)
    }
    /// Inserts an element at an index in the array,
    /// shifting all suceeding elements forward.
    /// ### Usage
    /// ```
    /// arr := [1, 3, 4, 5];
    /// arr.Insert(1, 2);
    /// Assert(arr.At(1)).Equals(Some(&2));
    /// ```
    public function Insert(index: UnsignedInt, value: T) {
        Invoke(Injunction.ArrInsert(index, value, this));
    }
    /// Inserts another array of items at the start of the array.
    /// shifting all its prior elements forward.
    /// ### Usage
    /// ```
    /// arr := [5, 6, 7, 8];
    /// arr.Prepend([1, 2, 3, 4]);
    /// Assert(arr).Equals([1, 2, 3, 4, 5, 6, 7, 8]);
    /// ```
    public function Prepend(prefix: []T) {
        Invoke(Injunction.ArrPrepend(prefix, this));
    }
    /// Returns the number of items in the array.
    /// ### Usage
    /// ```
    /// arr := [1, 2, 3, 4];
    /// Assert(arr.Length()).Equals(4);
    /// ```
    public function Length(): UnsignedInt {
        return Invoke(Injunction.ArrLength(this));
    }
    /// Adds an item to the end of the array.
    /// ### Usage
    /// ```
    /// array := [2, 4, 6, 8];
    /// Assert(array.Length()).Equals(4);
    /// 
    /// array.Push(10);
    /// Assert(array).Equals([2, 4, 5, 8, 10]);
    /// Assert(array.Length()).Equals(5);
    /// ```
    public function Push(value: T) {
        Todo()
    }
    /// Removes an item from the end of the array.
    /// ### Usage
    /// ```
    /// array := [1, 2, 3];
    /// 
    /// array.Pop();
    /// Assert(array).Equals([1, 2]);
    /// 
    /// array.Pop();
    /// Assert(array).Equals([1]);
    /// ```
    public function Pop(): ?T {
        Todo()
    }
    /// Removes the first item in the array
    /// and shifts all the other items backward
    /// by one index.
    /// 
    /// This operation has a time complexity of _O(n)_.
    /// ### Usage
    /// ```
    /// arr := [1, 2, 3, 4];
    /// firstItem := arr.Shift();
    /// 
    /// Assert(arr).Equals([2, 3, 4]);
    /// Assert(firstItem).Equals(Some(1));
    /// ```
    /// If there is no element in the array,
    /// then None() is returned, and the operation 
    /// does nothing.
    /// ```
    /// arr := [];
    /// firstItem := arr.Shift();
    /// 
    /// Assert(arr).Equals([]);
    /// Assert(firstItem).Equals(None());
    /// ```
    public function Shift(): ?T {
        Todo()
    }
    /// Returns a reference to the last item in the array.
    public function Last(): ?&T {
        length := this.Length();
        if length == 0 { None() } 
        else Some(&this[length - 1])
    }
    /// Converts an array to a string representation,
    /// given that its elements implement `Display`.
    /// ### Usage
    /// ```
    /// arr := [1, 6, 7, 8, 10];
    /// arrAsStr := Array.ToStr(&arr);
    /// 
    /// Assert(arrAsStr).Equals("[1, 6, 7, 8, 10]");
    /// ```
    public static function ToStr<T implements Display>(array: &Array<T>): String {
        string := "[";
        i := 0;
        array.Iter().Enumerate().ForEach(fn(tuple) {
            index := tuple.one;
            item := tuple.two;
            string.Append(item.ToStr());
            if index + 1 != array.Length() {
                string.Append(",")
            }
        });
        string.Append("]");
        string
    }
    /// Sorts an array in place, given that its elements are orderable.
    /// ### Usage
    /// ```
    /// arr := [34, 21, 89, 11, 20, 46];
    /// Array.Sort(&arr)
    /// Assert(arr).Equals([11, 20, 21, 34, 46, 89]);
    /// ```
    public static function Sort<T implements Orderable>(array: &Array<T>) {
        Todo()
    }
    /// Returns a Sorted version of an array
    /// given that its elements are orderable.
    /// 
    /// Sorting is stable, meaning that elements that have equal ordering 
    /// remain in their original position.
    /// ### Usage
    /// ```
    /// arr := [26, 10, 35, 16, 87, 1, 12];
    /// arrSorted := Array.Sorted(&arr);
    /// 
    /// Assert(arrSorted).Equals([1, 10, 12, 16, 26, 35, 87]);
    /// ```
    public static function Sorted<T implements Orderable>(array: &Array<T>): Array<T> {
        Todo()
    }
    /// Fills an array with default values, given that the element type can be defaulted.
    /// ### Usage
    /// ```
    /// arr := [1, 2, 3];
    /// Array.Reset(&arr);
    /// Assert(arr).Equals([0, 0, 0])
    /// ```
    public static function Reset<T implements Default>(array: &Array<T>) {
        array.Iter().ForEach(fn(value) {
            var default: T;
            *value = default;
        })
    }
    /// Returns the largest value in an array.
    /// It returns an empty value if the array is empty.
    /// ### Usage
    /// ```
    /// array := [2, 3, 4, 5, 6, 7];
    /// largest := Array.Max(&array);
    /// 
    /// Assert(largest == Some(7));
    /// ```
    public static function Max<T implements Orderable>(array: &Array<T>): ?T {
        max := *array.First()?;
        array.IntoIter().ForEach(fn(value) max = Max(max, value));
        Some(max)
    }

    /// Returns the smallest value in an array.
    /// It returns an empty value if the array is empty.
    /// ### Usage
    /// ```
    /// array := [2, 3, 4, 5, 6, 7];
    /// smallest := Array.Min(&array);
    /// 
    /// Assert(smallest == Some(2));
    /// ```
    public static function Min<T implements Orderable>(array: &Array<T>): ?T {
        min := *array.First()?;
        array.IntoIter().ForEach(fn(value) min = Min(min, value));
        Some(min)
    }

    /// Returns the sum of all the elements in the array.
    /// If there are no elements in the array, it returns the default value of
    /// the given type.
    /// ### Usage
    /// ```
    /// array := [1, 2, 3, 4];
    /// summed = Array.Sum(&array)
    /// 
    /// Assert(summed == 10);
    /// ```
    public static function Sum<T implements Addition + Default>(array: &Array<T>): T {
        var sum: T;
        array.IntoIter().ForEach(fn(item) sum += item);
        sum
    }
}


/// An owned iterator built from the elements of an array.
public model ArrayIter<T> implements Iteratable<T> {
    var array: &Array<T>;
    var idx: UnsignedInt;
    new(array: &Array<T>) {
        this.array = array;
        this.idx = 0;
    }

    public function [Iteratable.Next](): Maybe<T> {
        length := this.array.Length();
        if length == this.idx {
            return None();
        } else {
            nextItem := this.array[this.idx];
            this.idx += 1;
            return Some(nextItem);
        }
    }
}

public model ArrayRefIter<T> implements Iteratable<&T> {
    var array: &Array<T>;
    var idx: UnsignedInt;
    new(array: &Array<T>) {
        this.array = array;
        this.idx = 0;
    }
    public function [Iteratable.Next](): ?&T {
        Todo()
    }
}

/// A fixed-size list of items.
public model StaticArray<T> {
    new(size: Int, values: Array<T>) {
        // Compiler built in.
    }
}