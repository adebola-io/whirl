module orderedSet;

use core.prelude.todo;
use core.primitives.ops.Addition;


/// A simple ordered pair of items. Elements within a tuple can be of any type,
/// and the order in which they are defined is preserved.
public model Tuple<T, U> {
    public var a: T;
    public var b: U;
    
    new(first: T, second: U) {
        this.a = first;
        this.b = second;
    }
    /// Returns a new tuple with the position of the two items in the tuple reversed.
    /// ### Usage
    /// ```
    /// use core.collections.Tuple;
    /// 
    /// tuple := new Tuple(true, "story");
    /// swapped := tuple.swap();
    /// assertThat(swapped == new Tuple("story", true));
    /// ```
    public function swap(): Tuple<U, T> {
        new Tuple(this.b, this.a)
    }
    /// Concatenates the elements of the pair into one.
    /// ### Usage
    /// ```
    /// tuple := pair("John ", "Doe");
    /// joined := Tuple.join(tuple);
    /// 
    /// assert(joined).equals("John Doe");
    /// ```
    public static function join<T implements Addition>(tuple: Tuple<T, T>): T {
        tuple.a + tuple.b
    }
}
/// Takes two arguments of any type and returns a `Tuple` containing them.
/// 
/// It is useful for grouping related data or for passing multiple values 
/// as a single unit.
/// 
/// ### Usage
/// ```
/// pair1 := pair(1, "apple");
/// pair2 := pair(true, [1, 2, 3]);
/// 
/// // Accessing elements of the tuple.
/// first := pair1.a; 
/// assert(first).equals(1);
/// 
/// second := pair1.b;
/// assert(second).equals("apple");
/// ```
public function pair<T, U>(a: T, b: U): Tuple<T, U> {
    return new Tuple(a, b);
}

/// A set containing three individual items.
public model Triple<T, U, V> {
    public var a: T;
    public var b: U;
    public var c: V;

    new(a: T, b: U, c: V) {
        this.a = a;
        this.b = b;
        this.c = c;
    }
}

/// A set containing four individual items.
public model Quadruple<T, U, V, S> {
    public var a: T;
    public var b: U;
    public var c: V;
    public var d: S;

    new(first: T, second: U, third: V, fourth: S) {
        this.a = first;
        this.b = second;
        this.c = third;
        this.d = fourth;
    }
    /// Groups the items in the quadruple to two tuples.
    /// ###  Usage
    /// ```
    /// use core.collections.{Quadruple, Tuple};
    /// 
    /// quadruple := new Quadruple(1, "Sefunmi", true, [35.5]);
    /// var { a as first, b as second } = quadruple.toTuple();
    /// 
    /// assert(first == new Tuple(1, "Sefunmi"));
    /// assert(second == new Tuple(true, [35.5]));
    /// ```
    public function toTuple(): Tuple<Tuple<T, U>, Tuple<V, S>> {
        return pair(pair(this.a, this.b), pair(this.c, this.d));
    }
}