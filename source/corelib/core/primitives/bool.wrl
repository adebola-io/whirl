module bool;

use core.prelude.Display;
use string.String;
use core.prelude.{some, none};
use ops.Negation;


/// A representation of boolean values, `true` or `false`.
// compiler built-in.
public model Bool implements Display + Negation<Bool> {
    public function [Display.toStr]() -> String {
        if this { "true" } else "false"
    }
    public function [Negation.negate]() -> Bool {
        if this { false } else true
    }
    /// Creates a `Maybe<T>` based on the value.
    /// ### Usage
    /// ```
    /// operationSuccessful := true;
    /// message := fn "Operation successful!";
    /// 
    /// optionalMessage := operationSuccessful.ifTrue(message);
    /// 
    /// assertThat(optionalMessage.isSome());
    /// ```
    public function ifTrue<T>(consequent: fn() -> T) -> ?T {
        if this { some(consequent()) } else { none() }
    }
}